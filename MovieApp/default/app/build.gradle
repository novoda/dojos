buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.0-beta3'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'com.novoda:gradle-build-properties-plugin:0.2'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.novoda.build-properties'

buildProperties {
    secrets {
        file project.file('secrets.properties')
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.0'

    defaultConfig {
        applicationId 'com.tobi.movies'
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 1
        versionName '1.0'
        testApplicationId 'com.tobi.movies.steps'
        testInstrumentationRunner getInstrumentation()

        buildConfigProperty 'API_KEY', buildProperties.secrets['API_KEY'].or("")

    }
    buildTypes {

        debug {
            buildConfigField 'String', 'TEST_TAGS', '"' + getTestTags() + '"'
            buildConfigField 'String', 'TEST_FEATURE', '"' + getTestFeature() + '"'
            buildConfigField 'String', 'TEST_SCENARIO', '"' + getTestScenario() + '"'
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'

    }
}


def getTestTags() {
    project.getProperties().get('tags') ?: ''
}

def getTestFeature() {
    project.getProperties().get('feature') ?: ''
}

def getTestScenario() {
    project.getProperties().get('scenario') ?: ''
}

def getInstrumentation() {
    project.hasProperty('cucumber') ? 'com.tobi.movies.utils.CucumberInstrumentation' : 'android.support.test.runner.AndroidJUnitRunner'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:appcompat-v7:25.1.0'
    compile 'com.android.support:design:25.1.0'
    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup.retrofit2:converter-gson:2.0.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.0'
    compile 'io.reactivex:rxjava:1.1.3'
    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.android.support:support-v4:25.1.0'
    provided 'javax.annotation:javax.annotation-api:1.2'
    compile 'com.google.auto.value:auto-value:1.2'
    compile 'joda-time:joda-time:2.9.4'


    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'com.google.guava:guava:18.0'

    // Espresso and Cucumber
    androidTestCompile 'com.android.support:support-annotations:25.1.0'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'

    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    androidTestCompile 'info.cukes:cucumber-android:1.2.2'
    androidTestCompile 'info.cukes:cucumber-picocontainer:1.2.0'
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.1') {
        exclude module: 'support-annotations'
        exclude module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }
}
